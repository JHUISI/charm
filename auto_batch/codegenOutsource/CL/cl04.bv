name := cl04
setting := asymmetric
N := 2

BEGIN :: types
 a := G2
 b := G2
 c := G2
 g := G1
 x := ZR
 y := ZR
 X := G1
 Y := G1
 m := ZR
 M := str
 sig := list{G2}
END :: types

BEGIN :: func:setup
input := list{None}
 g := random(G1)
output := g
END :: func:setup

BEGIN :: func:keygen
input := list{g}
 x := random(ZR)
 y := random(ZR)
 X := g^x
 Y := g^y
output := list{X, Y, x, y}
END :: func:keygen

BEGIN :: func:sign
input := list{x, y, M}
 a := random(G2)
 m := H(M, ZR)
 b := a^y
 c := a^(x + (m * x * y))
 sig := list{a, b, c}
output := sig
END :: func:sign

# sig := expand{a, b, c}
# pk := expand{X, Y}
BEGIN :: func:verify
input := list{X, Y, g, M, a, b, c}
 m := H(M, ZR)
BEGIN :: if
if {{ e(Y, a) == e(g, b) } and { (e(X, a) * (e(X, b)^m)) == e(g, c) }}
   output := True
else
   output := False
END :: if
END :: func:verify

BEGIN :: precompute
m := H(M, ZR)
END :: precompute

constant := g
public := list{X, Y}
signature := list{a, b, c}
message := m

BEGIN :: count
message_count := N
public_count := one
signature_count := N
END :: count

#verify := {{ e(g, b) == e(Y, a) } and { (e(X, a) * e(X, b)^m) == e(g, c) }}
verify := { e(Y, a) == e(g, b) } and { (e(X, a) * (e(X, b)^m)) == e(g, c) }
